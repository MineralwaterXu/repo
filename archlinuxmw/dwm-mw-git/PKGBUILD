# Maintainer: Mineralwater Xu <mineralwater@m1neralwater.com>

pkgname=dwm-mw-git
pkgver=6.2.r17.43c1110
pkgrel=2
pkgdesc="MineralwaterXu's build of dynamic window manager for X"
url="https://github.com/MineralwaterXu/dwm"
arch=('x86_64')
license=('MIT')
depends=('libx11' 'libxft')
optdepends=(
	'libxft-bgra: if dwm crashes when displaying emojis'
	'libxft-bgra-git: if dwm crashes when displaying emojis'
	'dmenu: program launcher'
	'st: terminal emulator')
makedepends=('git')
source=("dwm::git+https://github.com/MineralwaterXu/dwm.git")
sha256sums=('SKIP')

pkgver() {
	cd dwm
	echo "$(awk '/^VERSION =/ {print $3}' config.mk)".r"$(git rev-list --count HEAD)"."$(git rev-parse --short HEAD)"
}

build() {
	cd dwm
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd dwm
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
