# Maintainer: Mineralwater Xu <mineralwater@m1neralwater.com>

_pkgname=qbittorrent
pkgname=$_pkgname-mw
pkgver=4.3.4.1
pkgrel=1
arch=('x86_64')
url="https://www.qbittorrent.org"
license=('custom' 'GPL')
depends=('libtorrent-rasterbar-mw' 'qt5-base')
makedepends=('boost' 'qt5-tools' 'qt5-svg')
optdepends=('python: needed for torrent search tab')
source=("https://downloads.sourceforge.net/sourceforge/qbittorrent/$_pkgname-${pkgver}.tar.xz"{,.asc})
sha512sums=('d5ca3d540c751d9ffe986d7197a40e71560898c282ab636c478aa5003a1d6ff812f022f5e11c909fd54dc9de43f865b243a52bd348b26091dba5c481ea2c6193'
            'SKIP')
b2sums=('e3dac2227e2affa70e258e9086f24455a89299c4d7bb2e4209399b635786302630dd3159332ff1cc2a6111336f0c7c9b55907d0ebd71d2326d70a58a5930facc'
        'SKIP')
validpgpkeys=('D8F3DA77AAC6741053599C136E4A2D025B7CC9A2') # sledgehammer999 <sledgehammer999@qbittorrent.org>


build() {
  cd $_pkgname-${pkgver}

  # tell qmake not to break makepkg's debug/!strip options
  export QBT_ADD_CONFIG='nostrip'

  mkdir -p $_pkgname 
  pushd $_pkgname
  ../configure --prefix=/usr
  make
  popd
}

package() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar."
  depends+=('qt5-svg' 'hicolor-icon-theme')

  cd $_pkgname-${pkgver}/$_pkgname

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm644 "${srcdir}"/$_pkgname-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/$_pkgname/COPYING
}
