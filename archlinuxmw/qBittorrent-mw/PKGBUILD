# Maintainer: Mineralwater Xu <mineralwater@m1neralwater.com>

pkgbase=qbittorrent-mw
pkgname=('qbittorrent-mw' 'qbittorrent-mw-nox')
pkgver=4.3.9
pkgrel=7
arch=('x86_64')
url="https://www.qbittorrent.org"
license=('custom' 'GPL')
depends=('libtorrent-rasterbar-mw' 'qt5-base')
makedepends=('boost' 'qt5-tools' 'qt5-svg')
optdepends=('python: needed for torrent search tab')
provides=('qbittorrent')
conflicts=('qbittorrent')
source=("https://downloads.sourceforge.net/sourceforge/qbittorrent/qbittorrent-${pkgver}.tar.xz"{,.asc})
sha512sums=('19cff24f08306eda77d944f0388ffb192a6650089925a71142bd68c854f9493846008b02b155dedab0192eb975fdeeaba762d210478d5179f6ab7ba6aabe6a64'
            'SKIP')
b2sums=('1f0427bb5cf31237577e8bfa3270c33797b4e1239a69cf943ea829d6a414f738e8404cdbfa2bbd08734c496f11b9adea811799fe7e6824d7d007e6beb69b62a4'
        'SKIP')
validpgpkeys=('D8F3DA77AAC6741053599C136E4A2D025B7CC9A2') # sledgehammer999 <sledgehammer999@qbittorrent.org>

prepare() {
  cd qbittorrent-${pkgver}
}

build() {
  cd qbittorrent-${pkgver}

  # tell qmake not to break makepkg's debug/!strip options
  export QBT_ADD_CONFIG='nostrip'

  mkdir -p qbittorrent
  pushd qbittorrent
  ../configure --prefix=/usr
  make
  popd

  # Build nox variant
  mkdir -p "qbittorrent-nox"
  pushd "qbittorrent-nox"
  ../configure --prefix=/usr --disable-gui --enable-systemd
  make
}

package_qbittorrent-mw() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar."
  depends+=('qt5-svg' 'hicolor-icon-theme')
  provides=('qbittorrent')
  conflicts=('qbittorrent')

  cd qbittorrent-${pkgver}/qbittorrent

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm644 "${srcdir}"/qbittorrent-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}

package_qbittorrent-mw-nox() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar, w/o gui"
  provides=('qbittorrent-nox')
  conflicts=('qbittorrent-nox')

  cd qbittorrent-${pkgver}/qbittorrent-nox

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm644 "${srcdir}"/qbittorrent-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
