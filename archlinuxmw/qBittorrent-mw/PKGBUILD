# Maintainer: Mineralwater Xu <mineralwater@m1neralwater.com>

pkgbase=qbittorrent-mw
pkgname=('qbittorrent-mw' 'qbittorrent-mw-nox')
pkgver=4.3.6
pkgrel=1
arch=('x86_64')
url="https://www.qbittorrent.org"
license=('custom' 'GPL')
depends=('libtorrent-rasterbar' 'qt5-base')
makedepends=('boost' 'qt5-tools' 'qt5-svg')
optdepends=('python: needed for torrent search tab')
source=("https://downloads.sourceforge.net/sourceforge/qbittorrent/qbittorrent-${pkgver}.tar.xz"{,.asc}
	"https://gist.githubusercontent.com/MineralwaterXu/d4a771580f9c492c999f7e3dff943fc3/raw/3a7bdbfdf05eac2e2e30fb539ce37218cb9c8809/qbittorrent-4.3.6-15098-fix.patch")
sha512sums=('ee495f957e2714f9a054d223bd3cff991317824f28bd6e4bc9cabf0e9b89a13def9637cf79c5bb93990dd4737b8b393bf314f1f9a2261cd0737f342f9eb2d52f'
            'SKIP'
	    'SKIP')
b2sums=('b3861d93aec76f51c68c5f342869c1330cf419d1bcfe993e226d7a5082c4e529d56bab462c3616937920470feaf45d8405e6d60d166d36cdf088ac51664c8e05'
        'SKIP'
	'SKIP')
validpgpkeys=('D8F3DA77AAC6741053599C136E4A2D025B7CC9A2') # sledgehammer999 <sledgehammer999@qbittorrent.org>

prepare() {
  cd qbittorrent-${pkgver}

  # https://github.com/qbittorrent/qBittorrent/pull/15098
  patch -p1 -i ../qbittorrent-${pkgver}-15098-fix.patch
}

build() {
  cd qbittorrent-${pkgver}

  # tell qmake not to break makepkg's debug/!strip options
  export QBT_ADD_CONFIG='nostrip'

  mkdir -p qbittorrent
  pushd qbittorrent
  ../configure --prefix=/usr
  make
  popd

  # Build nox variant
  mkdir -p "qbittorrent-nox"
  pushd "qbittorrent-nox"
  ../configure --prefix=/usr --disable-gui --enable-systemd
  make
}

package_qbittorrent-mw() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar."
  depends+=('qt5-svg' 'hicolor-icon-theme')
  provides=('qbittorrent')
  conflicts=('qbittorrent')

  cd qbittorrent-${pkgver}/qbittorrent

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm644 "${srcdir}"/qbittorrent-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}

package_qbittorrent-mw-nox() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar, w/o gui"
  provides=('qbittorrent-nox')
  conflicts=('qbittorrent-nox')

  cd qbittorrent-${pkgver}/qbittorrent-nox

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm644 "${srcdir}"/qbittorrent-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
