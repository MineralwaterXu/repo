# Maintainer: Mineralwater Xu <mineralwater@m1neralwater.com>

pkgbase=qbittorrent-mw
pkgname=('qbittorrent-mw' 'qbittorrent-mw-nox')
pkgver=4.4.2
pkgrel=1
arch=('x86_64')
url='https://www.qbittorrent.org'
license=('custom' 'GPL')
depends=('libtorrent-rasterbar-mw' 'qt6-base')
makedepends=('cmake' 'boost' 'qt6-tools' 'qt6-svg')
optdepends=('python: needed for torrent search tab')
provides=('qbittorrent')
conflicts=('qbittorrent')
source=("https://downloads.sourceforge.net/sourceforge/qbittorrent/qbittorrent-${pkgver}.tar.xz"{,.asc})
sha512sums=('7f38f9d00535486e68cd0f7f46d0ca8855c8703f946ed144ebf9fbe7e9acdeb789228d0ebe0b8a5ec0d304d1efecfa59499eadff83d0cbd944ecb10b7206d79b'
            'SKIP')
validpgpkeys=('D8F3DA77AAC6741053599C136E4A2D025B7CC9A2') # sledgehammer999 <sledgehammer999@qbittorrent.org>

prepare() {
  cd qbittorrent-${pkgver}
}

build() {
  cmake -B build -S qbittorrent-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT6=ON
  cmake --build build

  cmake -B build-nox -S qbittorrent-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT6=ON \
    -DGUI=OFF \
    -DSYSTEMD=ON
  cmake --build build-nox
}

package_qbittorrent-mw() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar."
  depends+=('qt6-svg' 'hicolor-icon-theme')
  provides=('qbittorrent')
  conflicts=('qbittorrent')

  DESTDIR="${pkgdir}" cmake --install build
  install -Dm644 qbittorrent-${pkgver}/COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

package_qbittorrent-mw-nox() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar, w/o gui"
  provides=('qbittorrent-nox')
  conflicts=('qbittorrent-nox')

  DESTDIR="${pkgdir}" cmake --install build-nox
  install -Dm644 qbittorrent-${pkgver}/COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
