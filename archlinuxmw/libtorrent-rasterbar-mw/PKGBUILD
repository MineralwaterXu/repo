# Maintainer: Mineralwater Xu <mineralwater@m1neralwater.com>

_pkgname=libtorrent-rasterbar
pkgname=$_pkgname-mw
pkgver=1.2.16
pkgrel=1
epoch=2
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
url="https://www.rasterbar.com/products/libtorrent/"
arch=('x86_64')
license=('BSD')
depends=('boost-libs' 'openssl')
makedepends=('boost' 'cmake' 'ninja' 'python-setuptools')
options=('!emptydirs')
provides=('libtorrent-rasterbar')
conflicts=('libtorrent-rasterbar')
source=(https://github.com/arvidn/libtorrent/releases/download/v$pkgver/$_pkgname-$pkgver.tar.gz)
sha512sums=('2bad1bf1604f99b85567056f798bfb78cdd663612291536d1145141e61f843aced416f7608911793c948d77802ef5b3088197b914c100a4278c94033eb304d4b')

build() {
  cd "$_pkgname-$pkgver"

  cmake \
    -B "_build" \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DCMAKE_INSTALL_LIBDIR="lib" \
    -Dpython-bindings=ON \
    -Dboost-python-module-name="python" \
    ./
  make -C "_build"
}

package() {
  cd "$_pkgname-$pkgver"

  make -C "_build" DESTDIR="$pkgdir" install
  install -Dm644 "LICENSE" -t "$pkgdir/usr/share/licenses/$_pkgname/"
}
