# Maintainer: Mineralwater Xu <mineralwater@m1neralwater.com>

_pkgname=libtorrent-rasterbar
pkgname=$_pkgname-mw
pkgver=1.2.13
pkgrel=2
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
arch=('x86_64')
url="https://www.rasterbar.com/products/libtorrent/"
license=('BSD')
depends=('boost-libs' 'openssl')
makedepends=('boost' 'python' 'python-setuptools')
options=('!emptydirs')
provides=('libtorrent-rasterbar')
conflicts=('libtorrent-rasterbar')
source=(https://github.com/arvidn/libtorrent/releases/download/v$pkgver/$_pkgname-$pkgver.tar.gz)
sha512sums=('29af34109f8bab91be03cd86a82ef592a9d1ae1a99ef4e6468985ac8968c67f80996bc0ac3fe2478ee20627df52e44128ff28fe81e928f31148b8c7066d67ccb')

prepare() {
  cd $_pkgname-$pkgver

  # Avoid depending on newer processors
  sed -i 's/-msse4.2//' configure.ac

  autoreconf -if
}

build() {
  cd $_pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --enable-python-binding \
    --enable-examples \
    --disable-static \
    --with-libiconv \
    --with-boost-python=boost_python3
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"

  # Remove most example binaries
  rm "$pkgdir"/usr/bin/{*_test,*_tester,simple_client,stats_counters}
}
