# Maintainer: Mineralwater Xu <mineralwater@m1neralwater.com>

pkgname=st-mw-git
pkgver=0.8.4.r5.119ab88
pkgrel=1
pkgdesc="MineralwaterXu's build of simple terminal implementation for X"
url="https://github.com/MineralwaterXu/st"
arch=('x86_64')
license=('MIT')
depends=('libx11' 'libxft')
makedepends=('git')
source=("st::git+https://github.com/MineralwaterXu/st.git#branch=master")
sha256sums=('SKIP')

pkgver() {
	cd st
	echo "$(awk '/^VERSION =/ {print $3}' config.mk)".r"$(git rev-list --count HEAD)"."$(git rev-parse --short HEAD)"
}

build() {
	cd st
	make 
}

package() {
	cd st
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
